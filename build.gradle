buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}
version "4.0.0-SNAPSHOT"
group "org.grails.plugins"

apply plugin: "org.grails.grails-plugin"
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    console "org.grails:grails-console"
    profile "org.grails.profiles:plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-web-testing-support"
    testCompile 'net.bytebuddy:byte-buddy-dep:1.8.12'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'grails-cascade-validation'
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'grails-cascade-validation'
                description = """This plugin establishes a 'cascade' constraint property for validateable objects. If "cascade:true" is set on a nested object, the nested object's validate() method will be invoked and the results will be reported as part of the parent object's validation."""
                url = 'https://github.com/rmorrise/grails-cascade-validation'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'rmorrise'
                        name = 'Russell Morisse'
                        email = 'russell.morisey@gmail.com'
                    }
                    developer {
                        id = 'asoftwareguy'
                        name = 'Eric Kelm'
                    }
                    developer {
                        id = 'burtbeckwith'
                        name = 'Burt Beckwith'
                    }
                    developer {
                        id = 'ctoestreich'
                        name = 'Christian Oestreich'
                    }
                    developer {
                        id = 'virtualdogbert'
                        name = 'Virtual Dogbert'
                    }
                    developer {
                        id = 'sbglasius'
                        name = 'SÃ¸ren  Berg Glasius'
                        email = 'soeren@glasius.dk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/rmorrise/grails-cascade-validation.git'
                    developerConnection = 'scm:git:ssh://github.com:rmorrise/grails-cascade-validation.git'
                    url = 'https://github.com/rmorrise/grails-cascade-validation/tree/main'
                }
            }
        }
    }
}
